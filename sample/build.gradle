apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]

    defaultConfig {
        applicationId "com.qingmei2.sample"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

def dependRemote = false

dependencies {
    implementation rootProject.ext.dependencies["kotlin-android"]
    implementation rootProject.ext.dependencies["appcompat"]
    implementation(rootProject.ext.dependencies["design"]) {
        exclude group: 'androidx.recyclerview'
    }

    implementation rootProject.ext.dependencies["glide"]
    kapt rootProject.ext.dependencies["glide-compiler"]

    androidTestImplementation rootProject.ext.dependencies["espresso-core"]
    androidTestImplementation rootProject.ext.dependencies["espresso-contrib"]
    androidTestImplementation rootProject.ext.dependencies["espresso-intent"]
    androidTestImplementation rootProject.ext.dependencies["test-runner"]
    androidTestImplementation rootProject.ext.dependencies["test-rules"]

    if (dependRemote) {
        implementation "com.github.qingmei2:rximagepicker:$rootProject.ext.android.versionName"
        implementation "com.github.qingmei2:rximagepicker_support:$rootProject.ext.android.versionName"
        implementation "com.github.qingmei2:rximagepicker_support_zhihu:$rootProject.ext.android.versionName"
        implementation "com.github.qingmei2:rximagepicker_support_wechat:$rootProject.ext.android.versionName"
    } else {
        implementation project(':rximagepicker')
        implementation project(':rximagepicker_support')
        implementation project(':rximagepicker_support_wechat')
        implementation project(':rximagepicker_support_zhihu')
    }

    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["junit"]
}