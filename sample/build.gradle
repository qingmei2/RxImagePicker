apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]

    defaultConfig {
        applicationId "com.qingmei2.sample"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

def testJcenter = true

dependencies {
    implementation rootProject.ext.dependencies["kotlin-android"]
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["design"]
    implementation rootProject.ext.dependencies["glide"]
    kapt rootProject.ext.dependencies["glide-compiler"]

    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-idling-resource:3.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:3.0.2"
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'

    if (testJcenter) {
        implementation "com.github.qingmei2:rximagepicker:$rootProject.ext.android.versionName"
        implementation "com.github.qingmei2:rximagepicker_support:$rootProject.ext.android.versionName"
        implementation "com.github.qingmei2:rximagepicker_support_zhihu:$rootProject.ext.android.versionName"
        implementation "com.github.qingmei2:rximagepicker_support_wechat:$rootProject.ext.android.versionName"
    } else {
        implementation project(':rximagepicker')
        implementation project(':rximagepicker_support')
        implementation project(':rximagepicker_support_wechat')
        implementation project(':rximagepicker_support_zhihu')
    }

    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["junit"]
}

kapt {
    generateStubs = true
}